#!/usr/bin/env groovy
/*
 * Build script for gradle-packer-plugin
 * Copyright © 2016-2018  Basil Peace
 *
 * This file is part of gradle-packer-plugin.
 *
 * This plugin is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License
 * as published by the Free Software Foundation, either version 3
 * of the License, or (at your option) any later version.
 *
 * This plugin is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this plugin.  If not, see <https://www.gnu.org/licenses/>.
 */
plugins {
  id 'org.fidata.project.groovy' version '3.0.1'
  id 'org.fidata.plugin' version '3.0.1'
}

description = 'Runs Packer builds from Gradle'
tags.set(['packer', 'devops', 'vm'])
license = 'LGPL-3.0-or-later'
publicReleases = true
contacts {
  'grv87@yandex.ru' {
    moniker 'Basil Peace'
    github 'grv87'
    roles 'owner', 'developer'
  }
}

dependencies {
  implementation group: 'org.ysb33r.gradle', name: 'grolifant', version: '[0.8, 1['
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '[2.9, 3['
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '[2.9, 3['
  // implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '[2.9, 3['
  implementation group: 'com.samskivert', name: 'jmustache', version: '1.13'
  // implementation group: 'commons-io', name: 'commons-io', version: '2.5'
  implementation group: 'com.fasterxml.uuid', name: 'java-uuid-generator', version: '3.1.5'
  implementation group: 'com.amazonaws', name: 'aws-java-sdk-ec2', version: '[1.0, 2['
  implementation group: 'com.google.guava', name: 'guava', version: '25.1-jre'
}

gradlePlugin {
  plugins {
    keygenPlugin {
      id = 'org.fidata.packer-ифыу'
      implementationClass = 'org.fidata.gradle.packer.PackerBasePlugin'
    }
  }
  plugins {
    keygenPlugin {
      id = 'org.fidata.packer'
      implementationClass = 'org.fidata.gradle.packer.PackerPlugin'
    }
  }
}

tasks.codenarcMain.disabledRules.add 'JavaIoPackageAccess'

sourceSets {
  integTest {
    groovy.srcDirs = ['src/integTest/groovy']
    resources.srcDirs = ['src/integTest/resources']
    compileClasspath += sourceSets['main'].output
    runtimeClasspath += output + sourceSets['main'].output
  }
}

plugins.getPlugin('org.fidata.base.jvm').configureIntegrationTestSourceSetClasspath sourceSets['integTest']
plugins.getPlugin('org.fidata.base.jvm').addJUnitDependency sourceSets['integTest']
plugins.getPlugin('org.fidata.base.groovy').addGroovyDependency configurations[sourceSets['integTest'].implementationConfigurationName]

tasks.register('integTest', Test) {
  group = 'Verification'
  description = 'Runs integration tests'
  shouldRunAfter 'test'
  testClassesDirs = sourceSets['integTest'].output.classesDirs
  classpath = sourceSets['integTest'].runtimeClasspath
  reports.junitXml.setDestination new File(xmlReportsDir, 'integTest')
  reports.html.setDestination new File(htmlReportsDir, 'integTest')
}

tasks.named('functionalTest').configure {
  shouldRunAfter 'integTest'
}

dependencies {
  integTestImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: 'latest.release'
}

stutter {
  sparse = true
  java(8) {
    compatibleRange '3.1'
  }
  java(9) {
    compatibleRange '3.1'
  }
}

pluginBundle.plugins {
  packerBasePlugin {
    displayName = 'Packer base plugin'
  }
  packerPlugin {
    displayName = 'Packer plugin'
  }
}

